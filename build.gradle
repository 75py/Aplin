buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath "com.android.databinding:dataBinder:1.0-rc4"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    PUBLISH_VERSION_NAME = versionName()
    PUBLISH_VERSION_CODE = versionCode()
}

task bumpMajor << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'major', type: 'int', operation: '+', value: 1)
        entry(key: 'minor', type: 'int', operation: '=', value: 0)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
        entry(key: 'id', type: 'int', operation: '+', value: 1)
    }
    generateReadMe()
}

task bumpMinor << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'minor', type: 'int', operation: '+', value: 1)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
        entry(key: 'id', type: 'int', operation: '+', value: 1)
    }
    generateReadMe()
}

task bumpPatch << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'patch', type: 'int', operation: '+', value: 1)
        entry(key: 'id', type: 'int', operation: '+', value: 1)
    }
    generateReadMe()
}

task version << {
    println "Version name : " + versionName()
    println "Version code : " + versionCode()
}

String versionName() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('version.properties').newDataInputStream())
    return properties['major'] + "." + properties['minor'] + "." + properties['patch']
}

Integer versionCode() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('version.properties').newDataInputStream())
    return Integer.valueOf(properties['id'].toString())
}

void generateReadMe() {
    def template = file('README.md.template').text
    def result = template.replaceAll("%%version%%", versionName())
    file("README.md").withWriter { it << result }
}
