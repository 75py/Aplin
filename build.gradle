/*
 * Copyright 2015 75py
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath 'com.cookpad.android.licensetools:license-tools-plugin:1.0.0'
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.hiya:jacoco-android:0.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    publish_version_name = versionName()
    publish_version_code = versionCode()
}

task bumpMajor {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'major', type: 'int', operation: '+', value: 1)
            entry(key: 'minor', type: 'int', operation: '=', value: 0)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
            entry(key: 'id', type: 'int', operation: '+', value: 1)
        }
        generateReadme()
    }
}

task bumpMinor {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'minor', type: 'int', operation: '+', value: 1)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
            entry(key: 'id', type: 'int', operation: '+', value: 1)
        }
        generateReadme()
    }
}

task bumpPatch {
    doLast {
        ant.propertyfile(file: 'version.properties') {
            entry(key: 'patch', type: 'int', operation: '+', value: 1)
            entry(key: 'id', type: 'int', operation: '+', value: 1)
        }
        generateReadme()
    }
}

task version {
    doLast {
        println "Version name : " + versionName()
        println "Version code : " + versionCode()
    }
}

String versionName() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('version.properties').newDataInputStream())
    return properties['major'] + "." + properties['minor'] + "." + properties['patch']
}

Integer versionCode() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('version.properties').newDataInputStream())
    return Integer.valueOf(properties['id'].toString())
}

task generateReadme {
    doLast {
        generateReadme()
    }
}

void generateReadme() {
    def template = file('README.md.template').text
    def result = template.replaceAll("%%version%%", versionName())
    file("README.md").withWriter { it << result }
}
